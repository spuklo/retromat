import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

ext.mainClassName = "io.github.spuklo.retromat.Retromat"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'io.javalin:javalin:3.8.0'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.3.72'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.10.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.2'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'com.github.librepdf:openpdf:1.3.14'
    implementation 'com.natpryce:konfig:1.6.10.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

jar {
    manifest {
        attributes 'Main-class': mainClassName,
                'Implementation-Title': 'Retromat',
                'Implementation-Version': project.version,
                'Build-Time': LocalDateTime.now().format(DateTimeFormatter.ISO_DATE_TIME)
    }
}

task buildInfo(type: WriteProperties) {
    outputFile = file("$buildDir/resources/main/buildinfo.properties")
    property 'version', project.version
}

processResources.dependsOn ':frontend:build', buildInfo
build.dependsOn shadowJar
